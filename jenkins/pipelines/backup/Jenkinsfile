pipeline {

    agent any

    environment {
        TAG = "${BUILD_NUMBER}"
        DOCKERHUB_CREDENTIALS_ID = 'credentiale-dockerhub'
        DOCKERHUB_CREDENTIALS = credentials("${DOCKERHUB_CREDENTIALS_ID}")
        IMAGE_NAME = "${DOCKERHUB_CREDENTIALS_USR}/docker-backup"
        WORKDIR = "${env.WORKSPACE}"
    }

    stages {

        stage('Lint') {
            steps {
              dir("${WORKDIR}/scripts") {
                  sh 'printenv'
                  sh 'pip install flake8'
                  sh 'python3 -m flake8 backup.py'
              }
            }
        }

        stage('Testare cod Python') {
            steps {
                dir("${WORKDIR}/scripts") {
                    script {
                        if (fileExists('test_backup.py')) {
                            sh 'pip install -r requirements.txt'
                            sh 'python3 -m pytest -v --junitxml=report.xml'
                            junit 'report.xml'
                        } else {
                            echo "Nu există fișier de test, se trece peste etapa de testare."
                        }
                    }
                }
            }
        }

        stage('Build imagine Docker') {
            steps {
                script {
                    dir("${WORKDIR}") {
                        dockerImage = docker.build("${IMAGE_NAME}:${TAG}", "-f docker/backup/Dockerfile .")
            }
                }
            }
        }

        stage('Confirmare Push in Docker Hub') {
            steps {
                script {
                    def confirmare = 'Nu'
                    try {
                        timeout(time: 2, unit: 'MINUTES') {
                            confirmare = input(
                                message: 'Vrei să publici imaginea de backup în Docker Hub?',
                                ok: 'Continuă',
                                parameters: [
                                    choice(name: 'CONFIRM', choices: ['Da', 'Nu'], description: 'Confirmă trimiterea imaginii în Docker Hub')
                                ]
                            )
                        }
                    } catch (err) {
                        echo "Timpul de răspuns a expirat. Se consideră alegerea: Nu"
                    }

                    echo "Alegere finală: ${confirmare}"
                    if (confirmare == 'Da') {
                        env.PUSH_TO_DOCKERHUB = 'true'
                    }
                }
            }
        }

        stage('Push in Docker Hub') {
            when {
                expression { return env.PUSH_TO_DOCKERHUB == 'true' }
            }
            steps {
                script {
                    docker.withRegistry('https://index.docker.io/v1/', "${DOCKERHUB_CREDENTIALS_ID}") {
                        dockerImage.push()
                        dockerImage.push('latest')
                    }
                }
            }

            post {
                success {
                    echo "Push-ul a fost efectuat cu succes pentru imaginea de backup!"
                    echo "Vezi imaginea: https://hub.docker.com/repository/docker/${IMAGE_NAME}"
                }
                failure {
                    echo "Push-ul a eșuat! Verifică logurile din build."
                }
            }
        }
    }

    post {
        always {
            cleanWs()
        }
    }
}
